# CAPITOLO 2 #
## Le classi in CoffeeScript ##

Per alcuni puristi, le classi in JavaScript hanno lo stesso effetto che gli spicchi d'aglio hanno su Dracula, anche se, ad essere onesti, non devi avere una propensione del genere, visto che stai leggendo un libro su CoffeeScript. Tuttavia, scoprirai che le classi sono altrettanto utili in JavaScript come sono in altri linguaggi e CoffeeScript offre un
grande astrazione in questo senso.

Dietro le quinte, CoffeeScript sta utilizzando la nativa natura prototipale di JavaScript per creare classi; aggiungendo una semplice sintassi (*sugar sintax*) per l'ereditarietà delle proprietà statiche ed il concetto di contesto di persistenza. Come sviluppatore, tutto ciò che ti serve è la keyword `class`:

	class Animale

Nell'esempio precedente, `Animale` è il nome della classe, e anche il nome della risultante variabile che è possibile utilizzare per creare istanze. Dietro le quinte, CoffeeScript sta usando dei costruttori, il che significa che è possibile istanziare le classi mediante l'operatore `new`:

	animale = new Animale

Definire i costruttori (funzioni che vengono richiamate alla creazione di un'istanza) è semplice: basta utilizzare una funzione denominata `constructor`. Questo modo di procedere è simile all'utilizzo di `initialize` di Ruby o di `__init__` du Python:

	class Animale
	  constructor: (nome) ->
	    @nome = nome

In realtà, CoffeeScript ha una scorciatoia per creare le proprietà di una istanza. Anteponendo argomenti con `@`, CoffeeScript imposterà automaticamente gli argomenti come proprietà di istanza nel costruttore. In effetti, questo trucco funziona bene anche con funzioni al di fuori delle normali Classi esterne. L'esempio seguente è equivalente all'ultimo esempio, dove abbiamo impostato manualmente le proprietà di istanza:

	class Animale
	  constructor: (@nome) ->

Come ci si aspetterebbe, gli argomenti passati alla creazione delle istanze sono passate al costruttore:

	animale = new Animale ("Pappagallo")
	alert "L'animale è un #{animale.nome}"

## Proprietà di istanza ##

L'aggiunta di ulteriori proprietà all'istanza di una classe è molto semplice: è esattamente la stessa sintassi dell'aggiunta di proprietà di un oggetto. Basta assicurarsi che le proprietà sono indentate correttamente all'interno del corpo della classe:

	classe Animal
	  prezzo: 5
	  vendita: (cliente) ->

    animale = new Animale
    animale.vendita(new Cliente)

I cambiamenti di contesto sono all'ordine del giorno all'interno JavaScript, e nel capitolo 1, abbiamo visto come
CoffeeScript può bloccare il valore di `this` per un particolare contesto utilizzando la freccia grassa: `=>`. Questo ci assicura che sotto qualsiasi contesto in cui una funzione viene chiamata, sarà sempre eseguita all'interno del contesto in cui è stata scritta. CoffeeScript ha esteso il supporto per le frecce grasse alle classi, quindi utilizzare una freccia grassa garantisce che il metodo dell'istanza sarà invocato nel contesto corretto, e che questo è sempre uguale a quello della istanza corrente:

	class Animale
	  prezzo: 5
	  vendita: =>
	    alert "Dami #{@price} scellini!"
	animale = new Animale
	$("#vendita").click(animale.vendita)

Come dimostrato nell'esempio sopra, questo è particolarmente utile per le callback degli eventi. Normalmente, la funzione `vendita()` sarebbe invocata nel contesto dell'elemento `#vendita`. Tuttavia, utilizzando le frecce grasse per `vendere()`, ci stiamo garantendo che essa sia eseguita nel giusto contesto e che `this.prezzo` sia uguale a `5`.

## Proprietà statiche ##

Cosa succede quando definiamo delle proprietà (ad esempio, statiche) in una classe? Beh, si scopre che all'interno di una definizione di classe, `this` si riferisce all'oggetto della classe. In altre parole, è possibile impostare le proprietà della classe impostandole direttamente su `this`.

	class Animale
	  this.trova = (nome) ->

	Animale.trova("Pappagallo")

Infatti, come ricorderai, `@` è un alias per `this` in CoffeeScript, il che consente di scrivere proprietà statiche ancora più velocemente:

	class Animale
	  @trova = (nome) ->

	Animale.trova("Pappagallo")

## Ereditarietà e Super ##

Non sarebbe una implementazione corretta delle classi senza una qualche forma di eredità, e CoffeeScript non ci delude. È possibile ereditare da un'altra classe utilizzando la keyword `extends`. Nell'esempio riportato di seguito, `Pappagallo` estende `Animale`, ereditando tutte le sue proprietà di istanza, come `vivo()`:

class Animale
  constructor: (@nome) ->
  vivo: ->
    false

class Pappagallo extends Animale
  constructor: ->
    super ( "Pappagallo" )
  dead: ->
    not vivo@()

Noterai che nel precedente esempio, stiamo usando la keyword `super()`. Dietro le quinte, questo si traduce in una chiamata di funzione sul prototipo padre della classe estesa, invocata nel contesto attuale. In questo caso, sarà `Pappagallo.__super__.constructor.call (this, "Pappagallo");`. In pratica, questo avrà esattamente lo stesso che ha chiamare `super` in Ruby o Python, invocando la funzione ereditata sovrascritta.

A meno che non si sovrascriva il costruttore `constructor`, per impostazione predefinita CoffeeScript invocherà il costruttore del padre quando vengono create nuove istanze.

CoffeeScript utilizza l'ereditarietà prototipale per ereditare automaticamente tutte le proprietà dell'istanza di una classe. Questo ci assicura che le classi sono dinamiche, anche se si aggiungono oggetti alla classe padre dopo che una classe figlia è stato creata, la proprietà sarà propagata a tutti i figli:

	class Animale
	  constructor: (@nome) ->
	
	class Pappagallo extends Animale
	
	Animale::rip = true
	
	pappagallo = new Pappagallo("Macaw")
	alert("Questo pappagallo c'è più") if pappagallo.rip

Vale la pena sottolineare però che le proprietà statiche vengono copiate  in sottoclassi, anziché ereditare utilizzando il prototipo come proprietà d'istanza. Ciò è dovuto ad alcuni dettagli d'implementazione con l'architettura prototipale di JavaScript, ed è un problema difficile da superare.

## Mixins ##

I Mixins non sono qualcosa di supportati nativamente da CoffeeScript, per la buona ragione che essi possono essere banalmente creati quando servono. Ad esempio, ecco due funzioni, `extend()` e `include()`, che aggiungono proprietà statiche e di istanza, rispettivamente, a una classe:

	extend = (obj, mixin) ->
	  obj[name] = method for name, method of mixin        
	  obj
	include = (klass, mixin) ->
	  extend klass.prototype, mixin
	# Utilizzo
	include Pappagallo,
	  eDeceduto: true
	(new Pappagallo).eDeceduto

I Mixins sono un ottimo strumento per la condivisione della logica comune tra i moduli quando l'ereditarietà non è adatta. Il vantaggio dei mixin è che è possibile includere più classi rispetto all'eredità dove solo una classe può essere ereditata.

## Estendere Classi ##

I Mixins sono molto carini, ma non sono granché orientato agli oggetti. Invece, cerchiamo di integrare i mixins in classi di CoffeeScript. Stiamo andando a definire una classe chiamata `Module` che può ereditare da un mixin. `Module` avrà due funzioni statiche, `@extend()` e  `@include()`, che possiamo utilizzare per estendere la classe con proprietà statiche e d'istanza, rispettivamente:

	moduleKeywords = ['extended', 'included']
	class Module
	  @extend: (obj) ->
	    for key, value of obj when key not in moduleKeywords
	      @[key] = value
	    obj.extended?.apply(@)
	    this
	  @include: (obj) ->
	    for key, value of obj when key not in moduleKeywords
	      # Assigno le proprietà al prototype
	      @::[key] = value
	    obj.included?.apply(@)
	    this

La piccola danza attorno alla variabile `moduleKeywords` è fatta per garantire di avere una callback quando i mixins estendono una classe. Diamo uno sguardo alla nostra classe `Module` in azione:

	classProperties = 
	  find: (id) ->
	  create: (attrs) ->
	instanceProperties =
	  save: -> 
	class User extends Module
	  @extend classProperties
	  @include instanceProperties
	# Utilizzo:
	user = User.find(1)
	user = new User
	user.save()

Come puoi vedere, abbiamo aggiunto alcune proprietà statiche , `trova()` e `crea()` alla classe utente, così come una proprietà d'istanza, `save()`. Dal momento che abbiamo delle callback ogni volta che i moduli sono estesi, possiamo abbreviare l'aggiunta di entrambi i tipi di proprietà:

	ORM = 
	  find: (id) ->
	  create: (attrs) ->
	  extended: ->
	    @include
	      save: -> 
	class User extends Module
	  @extend ORM

Super semplice ed elegante!