# CAPITOLO 3 #
## Idiomi ##

Ogni linguaggio ha una serie di idiomi e pratiche, e CoffeeScript non fa eccezione. Questo capitolo esplorerà tali convenzioni, e mostrerà alcune differenze fra Javascript e CoffeeScript in modo da avere un senso pratico del linguaggio.

## Each ##

In JavaScript, per scorrere ogni elemento in un array, possiamo utilizzare la nuova funzione `forEach()`, o il `for` secondo vecchio stile C. Se hai intenzione di utilizzare alcuni delle ultime caratteristiche di JavaScript introdotte in ECMAScript 5, consiglio di includere anche [shim](https://github.com/kriskowal/es5-shim "shim") nella pagina per supportare anche browser meno recenti:

	var dim = array.length;
	for (var i=0; i < dim; i++)
	  miaFunzione(array[i]);

	array.forEach(function(elemento, i){
	  miaFunzione(elemento)
	});

Anche se la sintassi `foreach()` è più sintetica e leggibile, soffre di un inconveniente, ovvero la funzione di callback deve essere richiamata ad ogni iterazione dell'array, il che la rende molto lenta rispetto all'equivalente ciclo `for`.  Vediamo come appare in CoffeeScript:

	miaFunzione(elemento) for elemento in array

Si tratta di una sintassi leggibile e concisa, sono sicuro che sarai d'accordo, e ciò che la rende fantastica è che viene compilata in un ciclo `for` dietro le quinte. In altre parole, CoffeeScript offre la stessa espressività di un `forEach()`, ma senza i problemi di velocità e retrocompatibilità.

## Map ##

Come con `forEach()`, ES5 include anche una funzione `map` nativa che ha una sintassi più concisa rispetto al classico ciclo `for`, cioè la `map()`. Purtroppo, soffre  degli stessi problemi del `forEach()` (cioè , la sua velocità è notevolmente ridotta a causa della chiamate a funzione):

	var risultato = [],
	    dim = array.length;
	for (var i=0; i < dim; i++)
	  risultato.push(array[i].nome)

	var result = array.map(function(elemento, i){
	  return elemento.nome;
	});

Come abbiamo detto nel Capitolo 1, il supporto al Comprehension di CoffeeScript può essere utilizzato per ottenere lo stesso
comportamento di `map()`. Da notare che stiamo circondano la Comprehension con le parentesi, le quali sono assolutamente fondamentali per garantire che essa restituisca quello che ci aspetteremmo (vale a dire, l'array mappati):

	risultato = (elemento.nome for elemento in array)

## Filtri ##

Ancora una volta, ES5 ha una funzione `filter()` per ridurre gli array:

	var risultato = [],
	    dim = array.length;
	for (var i=0; i < dim; i++)
	  if (array[i].nome == "test")
	    risultato.push(array[i])

	result = array.filter(function(oggetto, i){
	  return oggetto.nome == "test"
	});

La sintassi base di CoffeeScript utilizza la keyword `when` per filtrare gli elementi con un confronto. Dietro le quinte, viene generato un ciclo `for`. L'intera esecuzione viene eseguita in una funzione anonima per evitare una perdita di scope e conflitti fra variabili pre-esistenti:

	risultato = (oggetto for oggetto in array when oggetto.nome is "test")

Non dimenticare di includere le parentesi, altrimenti il risultato sarà l'ultimo elemento dell'array. Le Comprehension di CoffeeScript sono così flessibili che ti permettono di creare robuste selezioni, come mostrato dal seguente esempio:

	passate = []
	fallite = []
	(if punteggio > 60 then passate else fallite).push punteggio for punteggio in [49, 58, 76, 82, 88, 90]
	
	# Oppure
	passate = (punteggio for punteggio in punteggi when punteggio > 60)

Se le Comprehension diventano troppo lunghe, è possibile dividerle su più righe:

	passate = []
	fallite = []
	for punteggio in [49, 58, 76, 82, 88, 90]
	  (if punteggio > 60 then passate else fallite).push punteggio

## Inclusioni ##

Controllare se un valore è all'interno di un array, viene fatto in genere con `indexOf()`, che però richiede ancora uno shim, poiché Internet Explorer non lo supporta:

var incluso = (array.indexOf("test") != -1)

CoffeeScript ha una'alternativa a questo che i Pythonisti riconosceranno subito, vale a dire `in`:

	incluso = "test" in array

Dietro le quinte, CoffeeScript sta usando `Array.prototype.indexOf()` e lo shim se
necessario, per rilevare se il valore è all'interno dell'array. Sfortunatamente, questo significa che la stessa
sintassi `in` non funziona per le stringhe. Abbiamo bisogno di tornare a utilizzare `indexOf()` e provare che il risultato sia negativo:

	incluso = "a long test string".indexOf("test") isnt -1

O meglio ancora, dirottare all'operatore bit a bit la stringa in modo da non fare il confronto con `-1`.

	stringa  = "a long test string"
	incluso = !!~ string.indexOf "test"

## Proprietà Iterazione ##

Per iterare su un mucchio di proprietà in JavaScript, si utilizza l'operatore `in`. Per esempio:

	var oggetto = {uno: 1, due : 2}
	for (var chiave in oggetto) alert(tasto + "=" + oggetto[chiave])

Tuttavia, come visto nella sezione precedente, CoffeeScript ha già riservato in
l'uso di `in` per gli array. Invece, l'operatore originale `in` di JavaScript è stato rinominato `of` e può essere utilizzato in questo modo:

	oggetto = {uno: 1, due : 2}
	alert ("#{chiave} = #{valore}") for chiave, valore of oggetto

Come puoi vedere, è possibile specificare le variabili sia per il nome della proprietà che per il suo valore, cosa assai conveniente.

## Min/Max ##

Questa tecnica non è strettamente specifica a CoffeeScript, ma ho pensato che sia utile da sviscerare comunque. `Math.max` e `Math.min` prendono più argomenti, in modo da poter usare facilmente `...` per
passare loro un array, recuperando i valori massimi e minimi nell'array:

	Math.max [14, 35, -7, 46, 98]... # 98
	Math.min [14, 35, -7, 46, 98]... # -7

Vale la pena notare che questo trucco non viene eseguito con array davvero grandi, poiché i browser hanno una limitazione della quantità di argomenti passabili alle funzioni.

## Argomenti Multipli ##

Nell'esempio di `Math.max` qui sopra, abbiamo usato `...` per de-strutturare l'array e passare più argomenti a `max`. Dietro le quinte, CoffeeScript sta convertendo la chiamata funzione a utilizzando `apply()`, assicurandoci che l'array venga passato sottoforma di più argomenti a `max`. Possiamo utilizzare questa funzionalità anche in altri modi, come ad esempio per delegare delle chiamate di funzioni (*proxy*):

Log =
  log: ->
    console?.log(arguments...)

Oppure è possibile modificare gli argomenti prima che siano effettivamente alla funzione:

	Log =
	  logPrefix: "(App)"
	  log: (args...) ->
	    args.unshift(@logPrefix) if @logPrefix
	    console?.log(args...)

Tieni a mente però che CoffeeScript imposta automaticamente il contesto della chiamata a funzione all'oggetto funzione su cui viene richiamato. Nell'esempio sopra, l'oggetto in questione è `console`. Se desideri impostare un contesto specifico, allora hai bisogno di chiamare `apply()` manualmente.

## And/Or ##

CoffeeScript indica che `or` è preferibile a `||` e `and` è preferibile rispetto `&&`. Questo per un fatto di leggibilità. Ciononostante, i due stili hanno risultati identici . Questa preferenza verso un linguaggio naturale vale anche per nell'utilizzare `is` invece di `==` e `isnt` piuttosto che `=!`:

	stringa = "migrating coconuts"
	stringa == stringa # true
	stringa is stringa # true

Un piacevole aggiunta a CoffeeScript è la "or uguale", che i Rubysti conoscono come `||=`:

	hash or= {}

Se `hash` restituisce `false`, allora è un oggetto vuoto. E' qui importante notare 
che questa espressione riconosce anche `0`, `""`, e `null` come `false`. Se questa non è nostra intenzione, allora bisogna utilizzare l'operatore esistenziale di CoffeeScript, che viene attivata solo se hash è `undefined` o `null`:

	hash ?= { }

## Assegnazioni Destrutturate ##

Le Assegnazioni Destrutturate possono essere utilizzate con qualsiasi gamma di profondità e nidificazione per contribuire a tirare fuori oggetti nidificati:

	unOggetto = { a: 'value for a', b: 'value for b' }
	{ a, b } = unOggetto
	console.log "a e\' '#{a}', b e\' '#{b}'"

Questo è particolarmente utile in applicazioni per NodeJS che richiedono altri moduli:
	
	{join, resolve} = require('path')
	join('/Users', 'Alex')

## Librerie esterne ##

Utilizzare librerie esterne è esattamente lo stesso di utilizzare le stesse librerie in JavaScript, soltanto che bisogna scriverle con la sintassi di CoffeeScript. Questo perché, alla fine della giornata, tutto viene compilato in JavaScript. Usare CoffeeScript con jQuery è particolarmente elegante, grazie alla quantità di callback presenti nelle API di jQuery:

	# Usa alias locale
	$ = JQuery
	$ ->
	  # DOMContentLoaded
	  $(".el").click ->
	    alert("Cliccato!")

Dal momento che tutta l'output di CoffeeScript è circondato da una funzione anonima, possiamo impostare `$` come alias locale per jQuery. Questo farà in modo che, anche se è abilitata la modalità conflitto di jQuery, il nostro script continuerà a funzionare come previsto, visto che abbiamo ridefinito l'alias localmente.

## Variabili private ##

La keyword `do` in CoffeeScript ci permette di eseguire immediatamente le funzioni. Ciò è un ottimo modo di incapsulare lo scope e proteggere le variabili. Nell'esempio riportato di seguito, definiamo una
variabile `classToType` nel contesto di una funzione anonima che è immediatamente
chiamato con `do`. Tale funzione anonima restituisce una seconda funzione anonima, che sarà il valore finale di `type`. Poiché `classToType` è definita in un contesto in cui non viene mantenuto nessun riferimento, non può essere letta al di fuori di tale ambito:

	# Eseguire subito funzione
	type = do ->
	  types = [
	    "Boolean"
	    "Number" 
	    "String"
	    "Function" 
	    "Array" 
	    "Date" 
	    "RegExp"
	    "Undefined" 
	    "Null"
	  ]

	  classToType = {}
	  for name in types
	    classToType["[object " + name + "]"] = name.toLowerCase()

	  # Restituisce una funzione
	  (obj) ->
	    strType = Object::toString.call(obj)
	    classToType[strType] or "object"

In altre parole, `classToType` è completamente privata, e non può più essere utilizzata al di fuori della funzione anonima in esecuzione. Questo pattern è un ottimo modo di incapsulare lo scope e nascondere le variabili.