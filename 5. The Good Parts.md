# CAPITOLO 5 #
## The Good Parts ##

JavaScript è una bestia difficile, e conoscere le parti che si dovrebbe evitare è altrettanto importante quanto conoscere le parti da usare. Come dice Sun Tzu, "conosci il tuo nemico", e questo è esattamente quello che andremo a fare nel capitolo, esplorando il lato buoi di JavaScript e rivelando alcuni dei mostri in agguato pronto a balzare sull'ignaro sviluppatore.

Come ho già detto nella Prefazione, la parte fantastica di CoffeeScript non risiede solo nella sua sintassi, ma nella sua capacità di risolvere alcune delle bruttezze di JavaScript. Tuttavia, il linguaggio non è un proiettile d'argento contro tutti spauracchi di JavaScript, e ci sono ancora alcune questioni di cui è necessario essere
conoscenza.

## Le parti non corrette ##

Anche se CoffeeScript risolve alcuni dei difetti di progettazione di JavaScript, non
può andare da solo molto lontano. Come detto in precedenza, CoffeeScript è strettamente limitato all'analisi statica, e non tocca in alcun modo, per motivi di prestazione, il codice a runtime. CoffeeScript utilizza un compilatore sorgente-a-sorgente, con l'idea che ogni istruzione CoffeeScript abbia una rispettiva istruzione JavaScript. CoffeeScript non fornisce un'astrazione su una qualsiasi delle keyword di JavaScript, come `typeof`, e come tale, alcuni difetti di progettazione nel design di JavaScript valgono anche per CoffeeScript.

Parleremo prima di alcune questioni che CoffeeScript non può risolvere, e quindi toccheremo alcuni difetti di progettazione JavaScript che CoffeeScript è in grado di bissare.

## eval ##

Anche se CoffeeScript rimuove alcune delle debolezze di JavaScript, altre caratteristiche sono necessariamente *cattive*, basta essere consapevoli dei loro difetti. Un esempio calzante è la funzione `eval()`. Anche se indubbiamente ha i suoi usi, si dovrebbe conoscere i suoi svantaggi, evitarla quando possibile. La funzione `eval()` esegue una stringa di codice JavaScript nello scope locale, e funzioni come `setTimeout()` e `setInterval()` possono prendere un stringa come primo parametro da valutare.

Tuttavia, come `with`, `eval()` causa un calo delle performance. Poiché il compilatore non ha idea di cosa c'è dentro fino al runtime, non può fare ottimizzazioni. Un'altra preoccupazione è relativa alla sicurezza. Se l'input è scorretto, `eval` può facilmente aprire il codice per attacchi di tipo injection. In quasi tutti i casi, se stai utilizzando `eval`, ci sono migliori e più sicure alternative (come ad esempio le parentesi quadre):

	# Non fare questo
	modello = eval(nomeModello)
	
	# Usa le parentesi quadre invece
	modello = window[nomeModello]

## typeof ##
L'operatore `typeof` è probabilmente il più grande difetto di progettazione di JavaScript, semplicemente perché fondamentalmente non corretto. In realtà, effettua semplicemente un controllo per vedere se un valore è `undefined`:

	typeof undefinedVar is "undefined"

Per tutti gli altri tipi di controllo del tipo, `typeof` fallisce piuttosto miseramente, restituendo risultati incoerenti a seconda del browser e come sono state create istanze. Questo non è qualcosa che CoffeeScript può correggere, dato che il linguaggio effettua una analisi statica
e non ha alcun tipo di controllo a runtime. Tu sei da solo qui.

Per illustrare il problema, ecco una tabella presa da [JavaScript Garden](http://bonsaiden.github.com/JavaScript-Garden/ "JavaScript Garden") che mostra
alcune delle principali incongruenze nella verifica del tipo della keyword:

	Value               Class      Type
	-------------------------------------
	"foo"               String     string
	new String("foo")   String     object
	1.2                 Number     number
	new Number(1.2)     Number     object
	true                Boolean    boolean
	new Boolean(true)   Boolean    object
	new Date()          Date       object
	new Error()         Error      object
	[1,2,3]             Array      object
	new Array(1, 2, 3)  Array      object
	new Function("")    Function   function
	/abc/g              RegExp     object
	new RegExp("meow")  RegExp     object
	{}                  Object     object
	new Object()        Object     object

Come si può vedere, a seconda del modo in cui si definisce una stringa, con gli apici o con la classe String, influenza il risultato di `typeof`. Logicamente `typeof` deve restituire `"string"` per entrambi i controlli, ma per quest'ultimo restituisce `"object"`. Purtroppo, le incongruenze peggiorano da lì.

Che cosa possiamo usare per il controllo del tipo in JavaScript? Beh, per fortuna
`Object.prototype.toString()` viene in soccorso qui. Se invochiamo tale funzione nel contesto di un oggetto specifico, tornerà il tipo corretto. Ecco un esempio di implementazione attuata dal `$.type` di jQuery:

	type = do ->
	  classToType = {}
	  types = [
	    "Boolean"
	    "Number"
	    "String"
	    "Function"
	    "Array"
	    "Date"
	    "RegExp"
	    "Undefined"
	    "Null"
	  ]

	  for name in types
	    classToType["[object #{name}]"] = name.toLowerCase()

	  (obj) ->
	    strType = Object::toString.call(obj)
	    classToType[strType] or "object"

	# Restituisce il tipo che ci aspettiamo:
	type("")         # "string"
	type(new String) # "string"
	type([])         # "array"
	type(/\d/)       # "regexp"
	type(new Date)   # "date"
	type(true)       # "boolean"
	type(null)       # "null"
	type({})         # "object"

Se vuoi sapere se una variabile è stata definita, avrai ancora bisogno di utilizzare `typeof`; in caso contrario, si riceve un `ReferenceError`:

	if typeof aVar isnt "undefined"
	  objectType = type(aVar)

O più sinteticamente con l'operatore esistenziale:

	objectType = type(aVar?)

In alternativa alla verifica del tipo, spesso è possibile utilizzare il duck typing e l'operatore esistenziale insieme, il che elimina la necessità di conoscere il tipo di un oggetto. Per esempio, ipotizziamo di voler inserire un valore in un array. Potremmo dire che, finché un "oggetto simile ad un array" implementi `push()`, dovremmo trattarlo come un array:

	unArray?.push? aValue

Se `unArray` è un oggetto diverso da una array, l'operatore esistenziale farà in modo che `push()` non venga mai chiamato.

## instanceof ##

La keyword `instanceof` è mal pensata quasi come `typeof`. Idealmente, `instanceof`
sarebbe confront il costruttore di due oggetti, restituendo un valore booleano se uno è un'istanza dell'altro. Tuttavia, in realtà, `instanceof` funziona solo quando si confrontano oggetti personalizzati. Quando si tratta di confrontare i tipi built-in, è inutile come `typeof`:

	new String("foo") instanceof String # true
	"foo" instanceof String             # false

Inoltre, anche `instanceof` non funziona quando si confrontano oggetti diversi nel browser. In realtà, instanceof restituisce solo un risultato corretto per
oggetti personalizzato, come le classi CoffeeScript:

	class Parent
	class Child extends Parent
	child = new Child
	child instanceof Child  # true
	child instanceof Parent # true

Assicurati di utilizzarlo solo per i propri oggetti o, meglio ancora, stai alla larga da esso.

## delete ##

La keyword `delete` può solo essere tranquillamente utilizzato per la rimozione di proprietà all'interno degli oggetti:

	unOggetto = { uno: 1 , due : 2 }
	delete unOggetto.one
	unOggetto.hasOwnProperty("uno") # falso

Qualsiasi altro uso, come ad esempio l'eliminazione di variabili o funzioni, non funzionerà:

	aVar = 1
	delete aVar
	typeof aVar # "intero"

E' un comportamento piuttosto strano, ma è qui. Se si desidera rimuovere un riferimento
ad una variabile, basta assegnare `null`:

	aVar = 1
	aVar = null

## parseInt ##

La funzione di JavaScript `parseInt()` può restituire risultati imprevisti se si passa una stringa ad esso senza specificare una base. Per esempio:

	# Restituisce 8, non 10!
	parseInt('010') is 8

Passa sempre una base alla funzione per farlo funzionare correttamente:

	# Usa 10 come base per un corretto risultato
	parseInt('010', 10) is 10

Questo non è qualcosa CoffeeScript può fare per te, devi semplicemente ricordarti di specificare sempre una base quando si utilizza `parseInt()`.

## Modalità Strict ##

La Modalità Strict è una nuova funzionalità di ECMAScript 5, che consente di eseguire un programma o una funzione JavaScript o in un contesto più rigoroso. Questo contesto rigoroso genera più eccezioni e avvertenze rispetto al contesto classico, offrendo agli sviluppatori qualche indicazione quando si 
allontanano dalle *best practice*, scrivono codice non-ottimizzabile o semplicemente commettono errori comuni.

In altre parole, la Modalità Strict riduce i bug, aumenta la sicurezza, migliora le prestazioni ed elimina alcune caratteristiche del linguaggio difficile da usare. Cosa c'è che non va? La Modalità Strict è attualmente supportato nei seguenti browser:

- Chrome >= 13.0
- Safari >= 5.0
- Opera >= 12.0
- Firefox >= 4.0
- Internet Explorer >= 10.0

Detto questo, la Modalità Strict è completamente compatibile con i vecchi browser. I programmi che la utilizzano lavorano bene anche al di fuori di essa.

## Cosa succede in Modalità Strict ##
 
La maggior parte delle modifiche introdotte in questa modalità riguardano la sintassi di JavaScript:

- Causa errori quando si hanno nomi duplicati negli argomenti passati a proprietà e funzioni;

- Causa errori per l'uso improprio dell'operatore `delete`;

- L'accesso a `arguments.caller` & `arguments.callee` genera un errore (per motivi di prestazioni);

- Utilizzare l'operatore `with` genererà un errore di sintassi;

- Alcune variabili come `undefinied` non sono più sovrascrivibili;

- Introduce keyword riservate aggiuntive, quali  `implements`,  `interface`,  `let`,
`package`, `private`, `protected`, `public`, `static`, e `yield`;

Tuttavia, la modalità strict cambia anche qualche comportamento a runtime:

- Le variabili globali sono esplicite (var viene sempre richiesto), il valore del `this` globale è indefinito;

- `eval` non può introdurre nuove variabili nel contesto locale;

- Le funzioni create con `function` devono essere definite prima di essere utilizzati (in precedenza, le funzioni può essere [definita ovunque](http://whereswalden.com/2011/01/24/new-es5-strict-mode-requirement-function-statements-not-at-top-level-of-a-program-or-function-are-prohibited/ "Definizione di function"));

- `arguments` è immutabile.

CoffeeScript rispetta molti dei requisiti della modalità strict, come ad esempio utilizzare sempre var quando si definiscono le variabili, ma rimane molto utile per attivare la modalità strict nei programmi CoffeeScript. Infatti, CoffeeScript sta facendo passi ulteriori, ed in futuro le versioni saranno verificate in conformità modalità strict in fase di compilazione.

## Usare la Modalità Strict ##

Tutto ciò da fare per attivare la modalità strict è avviare lo script o la funzione con la seguente stringa:

	->
	  "use strict"
	  # ... il tuo codice...

Basta solo la stringa "use strict". Non potrebbe essere più semplice ed è retrocompatibile. Diamo uno sguardo alla strict mode in azione. La seguente funzione
genererà un errore di sintassi in questa modalità, ma va senza problema nella modalità consueta:

	do ->
	  "use strict"
	  console.log(arguments.callee)

La modalità Strict ha rimosso l'accesso a `arguments.caller` e `arguments.callee`, poiché incidono sulle prestazioni, e quindi sollevano errori di sintassi quando sono utilizzati.

C'è un particolare problema da cui stare lontani quando si utilizza la modalità rigorosa, vale a dire la creazione di variabili globali. Il seguente esempio solleva un `TypeError` in modalità strict, ma funziona bene in un contesto normale, creando una variabile globale:

	do ->
	  "use strict"
	  class @Spine

La ragione di questa differenza è che in modalità strict, `this` non è `undefinied`, mentre normalmente si riferisce all'oggetto `window`. La soluzione è impostare in modo esplicito le variabili globali nell'oggetto `window`:

	do ->
	  "use strict"
	  class window.Spine

Se abiliti la modalità rigorosa, vale la pena notare che la modalità script non attiva abilita tutte le nuove funzionalità che non sono già possibile in JavaScript, e farà effettivamente rallentare il codice un po' dovendo la VM fare più controlli in fase di esecuzione. Dunque è consigliabile sviluppare con modalità strict, e distribuire alla produzione senza di essa.

## Le parti corrette ##

Ora che abbiamo coperto alcune delle problematiche di JavaScript che CoffeeScript non può risolvere, parliamo di un paio che CoffeeScript risolve. Dal mio punto di vista, le seguenti caratteristiche sono alcune fra i migliori motivi per utilizzare CoffeeScript, poiché rimuovono alcuni degli errori più comuni gli sviluppatori fanno durante la scrittura di JavaScript. Se questo è poco più di una discussione accademica, dovresti leggere il resto di questo capitolo, soprattutto quando si effettua il caso di utilizzare CoffeeScript!

### Un sottoinsieme di istruzioni JavaScript ###

La sintassi di CoffeeScript riguarda solo un sottoinsieme di JavaScript, le famose *Good Parts*, in modo che ci siano meno problemi. Prendiamo l'istruzione `with` per esempio. Questa istruzione è stata per lungo tempo "considerata nocivo" e dovrebbe essere evitata e mirava ad essere una scorciatoia per la scrittura di ricerche di proprietà ricorrenti sugli oggetti. Per esempio, invece di scrivere:

	dataObj.users.alex.email = "info@eribium.org";

Si potrebbe scrivere:

	with(dataObj.users.alex) {
	  email = "info@eribium.org";
	}

Mettendo da parte il fatto che non dovremmo avere un oggetto così pesante, in primo luogo, la sintassi è abbastanza pulito. Tranne che per una cosa. E' fonte di confusione per l'interprete JavaScript, che non sa esattamente cosa vuoi fare nel contesto di `with`, e forza quindi la ricerca dell'oggetto specificato in primo luogo in tutti i contesti.

Questo fa veramente male le prestazioni e significa l'interprete deve disattivare tutti i tipi di ottimizzazioni JIT. Inoltre, `with` le dichiarazioni non possono essere minimizzate utilizzando strumenti come [uglify-js](https://github.com/mishoo/UglifyJS "UglifyJS"). E' anche deprecato ed è stato rimosso dalle future versioni di JavaScript. Tutto sommato, è molto meglio proprio evitare di usarlo e CoffeeScript fa un ulteriore passo avanti per la sua eliminazione dalla sintassi: utilizzare `with` in CoffeeScript genererà un errore di sintassi.

## Variabili globali ##

Per impostazione predefinita, i programmi JavaScript sono eseguiti in un ambito globale, e per impostazione predefinita, tutte le variabili create sono in questo ambito globale. Se si desidera creare una variabile in ambito locale, in JavaScript è necessario indicare esplicitamente la parola chiave `var`.

	usersCount = 1;        // Global
	var groupsCount = 2;   // Global
	(function(){              
	  pagesCount = 3;      // Global
	  var postsCount = 4;  // Local
	})()

Questo è stata una decisione strana dato che la stragrande maggioranza del tempo si creano variabili locali rispetto a quelle globali, quindi perché non renderle di default? Così com'è, gli sviluppatori devono ricordarsi di mettere l'istruzione `var` prima che tutte le variabili che siano inizializzate, o affrontare bug strani quando le variabili sono accidentalmente in conflitto e si sovrascrivono a vicenda.

Fortunatamente, CoffeeScript viene in vostro soccorso eliminando l'implicita assegnazione della variabile globale del tutto. In altre parole, la parola chiave `var` è riservata in CoffeeScript, e causa un errore di sintassi quando utilizzato. Le variabili locali vengono create implicitamente per impostazione predefinita ed è molto difficile creare variabili globali senza assegnarle esplicitamente a `window`.

Diamo uno sguardo ad un esempio di assegnazione di variabili di CoffeeScript:

	outerScope = true
	do ->
	  innerScope = true

Compila in:

	var outerScope;
	outerScope = true;
	(function() {
	  var innerScope;
	  return innerScope = true;
	})();

Notate come CoffeeScript inizializza variabili (usando `var`) automaticamente nel loro contesto prima dell'utilizzo. E anche se CoffeeScript rende difficile mascherare le variabili esterne, è possibile farvi ancora riferimento e accedervi. Hai bisogno di guardare fuori; stai attento, non riutilizzare il nome di una variabile esterna accidentalmente se stai scrivendo una funzione nidificata o una classe pesante. Ad esempio, qui stiamo per sovrascrivere accidentalmente la variabile del `package` in una funzione Class:

	package = require('./package')
	class Hem
	  build: ->
	    # Sovrascrive variabile esterna!
	    package = @hemPackage.compile()
	  hemPackage: ->
	    package.create()

Le variabili globali sono necessari di volta in volta, e per crearle è necessario impostarle come proprietà di `window`:

	class window.Asset
	  constructor: ->

Per garantire che le variabili globali siano esplicite, piuttosto che implicita, CoffeeScript rimuove una delle principali fonti di bug nei programmi JavaScript.

## Punto e virgola ##

JavaScript non impone l'uso del punto e virgola nel codice sorgente, quindi è possibile ometterli. Tuttavia, dietro le quinte, il compilatore JavaScript ne ha ancora bisogno, così il parser li inserisce automaticamente ogni volta che incontra un errore di analisi a causa di una mancata virgola. In altre parole, cercherà di valutare una dichiarazione senza punti e virgola e, se fallisce, tenta di nuovo con un punto e virgola.

Purtroppo, questa è una cattiva idea, e può effettivamente modificare il comportamento
del codice. Prendiamo il seguente esempio, che sembra valido JavaScript, giusto?

	function() {}
	(window.options || {}).property

Sbagliato. Beh, almeno secondo il parser; ma genera un errore di sintassi. In caso di una istruzione che inizia con la parentesi, il parser non inserirà un punto e virgola. Il codice si trasforma in una sola linea:

	function() {}(window.options || {}).property

Ora è possibile vedere il problema, e perché il parser si lamenta. Quando si sta scrivendo in JavaScript, si deve sempre includere un punto e virgola dopo dichiarazioni. Fortunatamente, CoffeeScript aggira tutto questo fastidio non avendo virgola nella sua sintassi. Piuttosto,
i punti e virgola vengono inseriti automaticamente (al posto giusto) quando CoffeeScript è compilato in JavaScript.

## Parole riservate ##

Alcune keyword in JavaScript sono riservati per le future versioni di JavaScript, come ad esempio `const`, `enum`, e `class`. L'utilizzo di questi nomi di variabili nei programmi JavaScript possono produrre risultati imprevedibili; alcuni browser li affrontano bene, altri no. CoffeeScript evita accuratamente la questione, rilevando l'utilizzando un parola riservata, e fuggire se necessario.

Ad esempio, diciamo di voler utilizzare la parola riservata `class` come proprietà di un oggetto. Il codice CoffeeScript potrebbe essere simile a questo:

	myObj = {
	  delete: "I am a keyword!"
	}
	myObj.class = ->

Il parser CoffeeScript si accorge che si sta utilizzando una parola riservata, e la rinomina per noi:

	var myObj;
	myObj = {
	  "delete": "I am a keyword!"
	};
	myObj["class"] = function() {};

## Operatori di Confronto ##

Gli operatori di confronto in JavaScript hanno un comportamento confuso e spesso sono fonte di bug problematici. L'esempio che segue è tratto dalla sezione "equality" del libro [JavaScript Garden](http://bonsaiden.github.com/JavaScript-Garden/#types.equality "JavaScript Garden"), che approfondisce la questione in profondità:

	""           ==   "0"           // false
	0            ==   ""            // true
	0            ==   "0"           // true
	false        ==   "false"       // false
	false        ==   "0"           // true
	false        ==   undefined     // false
	false        ==   null          // false
	null         ==   undefined     // true
	" \t\r\n"    ==   0             // true

La ragione di questo comportamento è che l'uguaglianza debole converte automaticamente i tipi. Sono sicuro che sarai d'accordo nel definire che tutto questo è abbastanza ambiguo, e può portare a risultati inaspettati e bug.

La soluzione è utilizzare invece l'operatore di uguaglianza rigorosa, che consiste in tre segni uguale (`===`). Funziona esattamente come l'operatore di uguaglianza classico, ma senza alcun tipo di coercizione del tipo. Si consiglia di utilizzare sempre l'operatore di uguaglianza rigorosa, e convertire i tipi se necessario esplicitamente.

CoffeeScript risolve questo problema semplicemente sostituendo tutti i confronti deboli con quelli rigorosi (in altre parole, la conversione di tutti gli operatori `==` in `===`). Non è possibile fare confronto con l'operatore di uguaglianza debole in CoffeeScript, e bisogna convertire in modo esplicito i tipi prima di confrontare
il tutto, se necessario.

Questo non significa che si può ignorare completamente la conversione del tipo implicita in CoffeeScript , è utile soprattutto quando si tratta di verificare la "veridicità" delle variabili durante il controllo di flusso.

Stringhe vuote, `null`, `undefined`, e il numero `0` sono tutti convertiti in `false`:

	alert("Empty Array")  unless [].length
	alert("Empty String") unless ""
	alert("Number 0")     unless 0

Se si desidera controllare esplicitamente `null` e `undefined`, è possibile utilizzare l'operatore esistenziale di CoffeeScript:

	alert("This is not called") unless ""?

L'`alert()` in questo esempio non sarà chiamato, poiché la stringa vuota non è uguale a `null`.

## Definizione di funzione ##

Stranamente, in JavaScript, le funzioni possono essere definite dopo che sono stati utilizzati. Il seguente esempio funziona correttamente, anche se `wem` è definito dopo che lo si chiama:

	wem();
	function wem() {}

Questo nasce dal contesto della funzione. Le funzioni vengono "sollevate" (in inglese *hoisted*) prima dell'esecuzione del programma e come tali sono disponibili in tutto il campo di applicazione in cui sono state definiti, anche se sono chiamate prima della definizione effettiva nel codice. Il guaio è, il comportamento del "sollevamento" differisce tra il browser. Per esempio:

	if (true) {
	  function declaration() {
	    return "first";
	  }
	} else {
	  function declaration() {
	    return "second";
	  }
	}
	declaration();

In alcuni browser (ad esempio Firefox), `declaration()` restituirà "`first`", e in altri browser (ad esempio, Chrome), restituisce  "`second`", come se l'altra istruzione non venga mai eseguita.

Se vuoi sapere di più sulle funzioni dichiarative, allora dovresti leggere la guida di [Juriy Zaytsev](http://kangax.github.com/nfe/ "Juriy Zaytsev") , dove sono approfondite le specifiche. Basti dire che hanno un comportamento abbastanza ambiguo, e può portare a problemi più tardi lungo la strada. Considerato tutto ciò, è meglio evitarlo ed utilizzare la sintassi  letterale:

	var wem = function(){};
	wem();

L'approccio di CoffeeScript a questo problema è quello di rimuovere le funzioni dichiarative del tutto, trasformandole in funzioni letterali.

## Proprietà di Number ##

Un difetto nel parser di JavaScript è che la `dot notation` sui numeri viene interpretato come virgola mobile letterale, piuttosto che come ricerca di proprietà. Ad esempio, il seguente codice JavaScript causa un errore di sintassi:

	5.toString();

Il Parser di JavaScript è alla ricerca di un altro numero dopo il punto, e così solleva un e errore `Unexpected token` quando rileva `toString()`. La soluzione a questo è o usare le parentesi, oppure aggiungere un punto aggiuntivo:

	(5).toString();
	5..toString();

Fortunatamente, il parser di CoffeeScript è abbastanza intelligente da affrontare questo problema utilizzando le notazioni doppio punto automaticamente (come nell'esempio precedente) ogni volta che l'accesso immobili in numeri.

## JavaScript Lint ##

[JavaScript Lint](http://www.javascriptlint.com/ "JavaScript Lint") è uno strumento per la rilevazione della qualità del codice JavaScript ed è un ottimo modo per migliorare il proprio codice utilizzando le best practice. Il progetto si è basato su uno strumento simile, chiamato  [JSLint](http://www.jslint.com/ "JSLint"). Visita il sito di JSLint per scovare una grande lista di problemi comuni, comprese le variabili globali, punti e virgola mancanti e confronti.

La buona notizia è che CoffeeScript pulisce già il codice che produce, in modo che il codice JavaScript generato è già JavaScript Lint compatibile. Infatti, il tool `coffee` supporta per l'opzione `--lint`:

	coffee --lint index.coffee
	  index.coffee: 0 error(s), 0 warning(s)