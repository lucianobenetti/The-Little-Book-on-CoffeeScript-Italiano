# CAPITOLO 4 #
## Compilare CoffeeScript ##

Un problema con CoffeeScript è che mette un altro strato tra te e JavaScript, ed è necessario compilare manualmente i file CoffeeScript ogni volta che cambiano rapidamente.
Fortunatamente, CoffeeScript ha alcune forme alternative di compilazione che possono rendere il ciclo di sviluppo un po' più agevole.

Come abbiamo coperto in "Setup Inizizale" a pagina VI, siamo in grado di compilare i file CoffeeScript usando l'eseguibile coffee:

	coffee --compile --output lib src

Tuttavia, scriverlo ogni volta che cambia un file è noioso, quindi diamo un'occhiata a come automatizzare questo processo.

## Cake ##

[Cake](http://jashkenas.github.com/coffee-script/#cake "Cake") è un super semplice sistema di generazione lungo le linee di [Make](http://www.gnu.org/software/make/ "Make") e [Rake](http://rake.rubyforge.org/ "Rake"). La libreria è in bundle con il pacchetto NPM `coffee-script`, ed è disponibile usando un eseguibile chiamato `cake`.

È possibile definire dei task utilizzando CoffeeScript in un file chiamato `Cakefile`. Cake li selezionarà, e può essere invocato eseguendo `cake [task] [options]` dall'interno della directory. Per stampare un elenco di tutti i task e le opzioni, basta digitare `cake`.

Le attività vengono definite utilizzando la funzione `task()`, passando un nome, una descrizione facoltativa, e funzione di callback. Ad esempio, creare un file chiamato `Cakefile`, e due directory, `lib` e `src`. Aggiungere quanto segue a `Cakefile`:

	fs = require 'fs'
	{print} = require 'util'
	{spawn} = require 'child_process'
	build = (callback) ->
	  coffee = spawn 'coffee', ['-c', '-o', 'lib', 'src']
	  coffee.stderr.on 'data', (data) ->
	    process.stderr.write data.toString()
	  coffee.stdout.on 'data', (data) ->
	    print data.toString()
	  coffee.on 'exit', (code) ->
	    callback?() if code is 0
	task 'build', 'Build lib/ from src/', ->
	  build()

Nel precedente esempio, stiamo definendo un task chiamato `build` che può essere richiamato eseguendo `cake build`. Questo esegue lo stesso task dell'esempio precedente, ovvero la compilazione di tutte i file CoffeeScript in `src` in `lib`. È ora possibile fare riferimento ai file JavaScript nella directory `lib` come al solito da HTML:

	<script src="lib/app.js" type="text/javascript" charset="utf-8"></script>

Dobbiamo ancora dover eseguire manualmente `cake build` ad ogni modifica al codice CoffeeScript, il che è ben lungi dall'essere ideale. Fortunatamente, il comando `coffee` prende un'altra opzione, `--watch`, che obbliga a guardare una directory per notare i cambiamenti e quindi ri-compilare se necessario. Andiamo a definire un'altra operazione utilizzando questo parametro:

	task 'watch', 'Watch src/ for changes', ->
	  coffee = spawn 'coffee', ['-w', '-c', '-o', 'lib', 'src']
	  coffee.stderr.on 'data', (data) ->
	    process.stderr.write data.toString()
	  coffee.stdout.on 'data', (data) ->
	    print data.toString()

Se un task si basa su un altro, è possibile eseguire altre task utilizzando `invoke(nome)`. Aggiungiamo un task di utilità nel nostro `Cakefile` che apre index.html e guardare le modifiche al sorgente:

	task 'open', 'Open index.html', ->
	  # First open, then watch
	  spawn 'open', 'index.html'
	  invoke 'watch'

È inoltre possibile definire le opzioni per l'operazione utilizzando la funzione `option()`, che prende una nome breve, nome completo e la descrizione del task:

	task 'build', 'Build lib/ from src/', ->
	  # Ora abbiamo accesso a una opzioni oggetto`
	  coffee = spawn 'coffee', ['-c', '-o', options.output or 'lib', 'src']
	  coffee.stderr.on 'data', (data) ->
	    process.stderr.write data.toString()
	  coffee.stdout.on 'data', (data) ->
	    print data.toString()

Come potete vedere, il contesto del task ha ora accesso a un oggetto che contiene tutte i dati specificati dall'utente. Se eseguiamo `cake` senza altri argomenti, verranno elencati tutti i task e le opzioni.

Cake è un ottimo modo di automatizzare i task più comuni come la compilazione CoffeeScript senza andare a disturbare `bash` o `Makefile`. Vale anche la pena dare un'occhiata ai [sorgenti di Cake](http://jashkenas.github.com/coffee-script/documentation/docs/cake.html "Sorgenti di Cake"), un grande esempio di espressività di CoffeeScript e ben documentata.

## Creazione di applicazioni ##

Utilizzando Cake per CoffeeScript la compilazione va bene per i siti statici, ma per i siti dinamici, potremmo anche integrare la compilazione di CoffeeScript nel ciclo di richiesta/risposta. Esistono già diverse soluzioni di integrazione per le framework backend popolari e ambiti, come [Rails](http://rubyonrails.org/ "Ruby on Rails") e [Django](https://www.djangoproject.com/ "Django").

Il resto di questo capitolo esamina come strutturare realmente e distribuire applicazioni CoffeeScript lato client. Se stai usando solo CoffeeScript sul lato server, o il tuo framework, come Rails, gestisce già tutto questo, sentitevi liberi di saltare al capitolo 5.

Per qualche ragione, quando gli sviluppatori creano applicazioni JavaScript lato client, anche se usano modelli e convenzioni riconosciuti, spesso volano fuori dalla finestra, ed il risultato finale è un
pasticcio non mantenibile di JavaScript accoppiati. Non posso sottolineare abbastanza quanto importante è una architettura applicativa, se si sta scrivendo qualsiasi JavaScript/CoffeeScript
oltre la semplice validazione dei form, è necessario implementare una forma di struttura dell'applicazione, come [MVC](http://en.wikipedia.org/wiki/Model%E2%80%93view%E2%80%93controller "MVC").

Il segreto per costruire applicazioni di grandi dimensioni gestibili non è quello di costruire applicazioni di grandi dimensioni.
In altre parole, costruire una serie di componenti disaccoppiati modulari. Tenere l'applicazione logica più generica possibile, astraendo fuori del caso. Infine, separare logica relativi alle view, modelli e controller (MVC). L'implementazione di MVC è al di là dello scopo di questo capitolo, per questo, vi consiglio di controllare il mio libro [JavaScript Web Application](http://shop.oreilly.com/product/0636920018421.do "JavaScript Web Application") e utilizzare un framework come [Backbone](http://documentcloud.github.com/backbone/ "Backbone") o [Spine](https://github.com/maccman/spine "Spine"). Più che questo, stiamo per introdurre come strutturare applicazioni utilizzando i moduli CommonJS.

## Struttura e CommonJS ##

Che cosa sono esattamente i moduli CommonJS? Beh, se hai mai usato NodeJS prima,hai usato dei moduli CommonJS probabilmente senza rendertene conto. I moduli CommonJS sono stati inizialmente sviluppati per la scrittura di librerie JavaScript lato server, nel tentativo di affrontare carico, namespacing, e le varie questioni di scope. Erano un formato comune compatibile con tutte le implementazioni JavaScript. L'obiettivo era che una libreria scritta
per Rhino avrebbe funzionato con Node. Alla fine queste idee sono tornate ai browser, e ora abbiamo grandi librerie come RequireJS e Yabble che ci aiutano ad usare moduli lato client.

In pratica, i moduli permettono che il codice venga eseguito in uno spazio dei nomi locale (incapsulamente), in modo che sia possibile caricare altri moduli con la funzione `require()`, mentre per esporre le proprietà del modulo si utilizza `module.exports`. Tuffiamoci più in profondità.

### Richiedere file ###

È possibile caricare altri moduli e librerie con `require()`. Basta passare un modulo
nome e, se si trova nel percorso da caricare, viene restituito un oggetto che rappresenta quel modulo. Per esempio:

	User = require("models/user")

Il supporto al require sincrono è una questione controversa, ma è stata in gran parte risolta con le librerie per il loader tradizionali e le ultime proposte CommonJS. E' qualcosa a cui prestare attenzione se hai deciso di prendere una strada distinta rispetto a quella esposta con gli Stitch qui sotto.

### Esportazione di proprietà ###

Per impostazione predefinita, i moduli non espongono alcuna proprietà, quindi il loro contenuto è completamente invisibile alle chiamate require(). Se si vuole rendere accessibile una particolare proprietà dal
modulo, è necessario impostare su `module.exports`:

	# random_module.js
	module.exports.myFineProperty = ->
	  # Alcune proprietà

Ora ogni volta che è necessario questo modulo, `myFineProperty` sarà esposto:

	myFineProperty = require("random_module").myFineProperty

## Stitch ##

Formattare il codice in moduli CommonJS è bene e carino, ma come si fa effettivamente ottenere a fare lavoro sul lato client in pratica? Beh, la mia scelta cade sulla libreria Stitch. Stitch nasce da Sam Stephenson, la mente dietro Prototype.js tra le altre cose, e risolve il problema dei moduli così elegantemente che mi vien voglia di ballare di gioia! Piuttosto che cercare di risolvere in modo dinamico le dipendenze, Stitch semplicemente unisce tutti i file in un unico file JavaScript, usando alcune magie CommonJS. Oh, e mi ha fatto ricordare che viene i file CoffeeScript, i template JS, [LESS CSS](http://lesscss.org/ "LESS") , e [Sass](http://sass-lang.com/ "Sass") vengono compilati.

Per prima cosa, hai bisogno di installare Node.js ed NPM se non l'hai già fatto. Faremo tutto ciò passo per passo in questo capitolo.

Ora creiamo la nostra struttura dell'applicazione. Se stai usando [Spine](https://github.com/maccman/spine "Spine"), è possibile automatizzare questo
con [Spine.App](http://github.com/maccman/spine.app "Spine.App"), altrimenti è bisogna fare tutto manualmente. Di solito ho
una cartella app per tutto il codice specifico dell'applicazione, e una cartella lib per le librerie generiche.
Poi tutto il resto, compresi i beni statici, vanno nella directory pubblica:

	app
	app/controllers
	app/views
	app/models
	app/lib
	lib
	public
	public/index.html

Ora avvieremo effettivamente il server Stitch. Creiamo un file chiamato index.coffee e riempiamolo con il seguente codice:

	require("coffee-script")
	stitch  = require("stitch")
	express = require("express")
	argv    = process.argv.slice(2)
	package = stitch.createPackage(
	  # Specify the paths you want Stitch to automatically bundle up
	  paths: [ __dirname + "/app" ]
	  # Specify your base libraries
	  dependencies: [
	    # __dirname + '/lib/jquery.js'
	  ]
	)
	app = express.createServer()
	app.configure ->
	  app.set "views", __dirname + "/views"
	  app.use app.router
	  app.use express.static(__dirname + "/public")
	  app.get "/application.js", package.createServer()
	port = argv[0] or process.env.PORT or 9294
	console.log "Starting server on port: #{port}"
	app.listen port

Potete vedere alcune dipendenze elencate: coffee-script, stitch, e express. Abbiamo bisogno di per creare un file `package.json`, elencando queste dipendenze in modo che npm possa raccoglierle. Il nostro file `./package.json` sarà simile a questo:

	{
	  "name": "app",
	  "version": "0.0.1",
	  "dependencies": { 
	    "coffee-script": "~1.1.2",
	    "stitch": "~0.3.2",
	    "express": "~2.5.0",
	    "eco": "1.1.0-rc-1"
	  }
	}

E installiamo queste dipendenze con NPM:

	npm install .
	npm install -g coffee-script

Perfetto, ci siamo quasi. Ora esegui:

	coffee index.coffee

Si spera che tu abbia un server Stitch attivo e funzionante. Andiamo avanti e verifichiamo il lavoro svolto damettendo uno script `app.coffee` nella cartella `app`. Questo sarà il file che farà da bootstrap per la nostra applicazione:

	module.exports = App =
	  init: ->
	    # Bootstrap the app

Ora creiamo la nostra pagina `index.html` principale che, se stiamo costruendo una app a pagina singola, sarà l'unica pagina in cui l'utente si sposta effettivamente. Questa è statico, quindi va messa nella directory `public`:

	<html>
	<head>
	  <meta charset=utf-8>
	  <title>Application</title>
	  <!-- Require the main Stitch file -->
	  <script src="/application.js" type="text/javascript" charset="utf-8"></script>
	  <script type="text/javascript" charset="utf-8">
	    document.addEventListener("DOMContentLoaded", function(){
	      var App = require("app");
	      App.init();
	    }, false);
	  </script>
	</head>
	<body>
	</body>
	</html>

Quando la pagina viene caricata, la nostra callback evento *DOMContentLoaded* sta richiedendo lo script `app.coffee` (che viene compilato automaticamente), e invocando la nostra funzione `init()`. Questo è tutto quello che c'è da fare. Abbiamo installato i moduli CommonJS e sono funzionanti, nonché un Server HTTP ed un compilatore CoffeeScript. Se, per esempio, avessimo voluto includere un modulo, basta chiamare la funzione `require()`. Creiamo una nuova classe, `User`, e vi facciamo riferimento da `app.coffee`:

	# app/models/user.coffee
	module.exports = class User
	  constructor: (@name) ->
	# app/app.coffee
	User = require("models/user")

## Template in JavaScript ##

Quando sposti la logica lato client, allora sicuramente hai bisogno di un qualche tipo di libreria di template. I template in JavaScript sono molto simili a quelli su server, come ERB di Ruby o l'interpolazione di testo in Python, tranne che stanno client. Ci sono tutta una serie di librerie per il templating là fuori, quindi ti incoraggio a fare un po' ricerca e test. Per impostazione predefinita, Stitch è dotato di supporto ai template [Eco](https://github.com/sstephenson/eco "Eco").

I template JavaScript sono molto simili a quelle lato server. Hai tag all'interno di HTML, e durante il rendering, questi tag vengono valutati e sostituiti. La cosa grandiosa dei template Eco è che sono effettivamente scritti in CoffeeScript.

Ecco un esempio:

	<% if @projects.length: %>
	  <% for project in @projects: %>
	    <a href="<%= project.url %>"><%= project.name %></a>
	    <p><%= project.description %></p>
	  <% end %>
	<% else: %>
	  Nessun progetto
	<% end %>

Come puoi vedere, la sintassi è estremamente semplice. Basta usare i tag `<%` per valutare espressioni e `<%=` tag per stamparli. Un elenco parziale dei tag è il seguente:

    <% istruzione %>

Valutar un'espressione CoffeeScript senza stampare il suo valore restituito.

	<%= istruzione %>

Valutare un'espressione CoffeeScript, stampando il suo valore ritornato effettuando escape.

    <%- istruzione %>

Valuta un'espressione CoffeeScript e stampa il valore restituito senza escape.

È possibile utilizzare qualsiasi espressione CoffeeScript all'interno dei tag di template, ma c'è una a cui stare attento. CoffeeScript è sensibile agli spazi bianchi, mentre i template Eco non lo sono. Pertanto, i tag Eco che iniziano un blocco CoffeeScript rientrato devono terminare con i due punti. Per indicare la fine di un blocco indentato, utilizza il tag speciale `<% end %>`. Per esempio:

	<% if @project.isOnHold(): %>
	  In attesa
	<% end %>

Non è necessario scrivere gli `if` e le chiusure su righe separate:

	<% if @project.isOnHold(): %> In attesa <% end %>

Ed è possibile utilizzare il modulo con l'`if` postposto in questo modo:

	<%= "In attesa" if @project.isOnHold() %>

Ora che abbiamo la sintassi, definire un modello di Eco in `views/users/show.eco`:

	<label>Name: <%= @name %></label>

Stitch compilerà automaticamente il nostro modello e lo include in `application.js`. Poi, nel controller della nostra applicazione, richiediamo il modello, come se fosse un modulo e lo eseguiamo passando tutti i dati richiesti:

	require("views/users/show")(new User("Brian"))

Il nostro file `app.coffee` dovrebbe ora assomigliare a questo, renderizzando il modello e aggiungendo il tutto alla pagina quando viene caricato il documento:

	User = require("models/user")
	App =
	  init: ->
	    template = require("views/users/show")
	    view     = template(new User("Brian"))
	    # Obviously this could be spruced up by jQuery
	    element = document.createElement("div")
	    element.innerHTML = view
	    document.body.appendChild(element)
	module.exports = App

Aprite l'applicazione e dare un vortice ! Speriamo che questo tutorial vi ha dato una
buona idea di come strutturare le applicazioni CoffeeScript lato client . Per i vostri prossimi passi ,
Vi consiglio di studiare un framework client-side come [Backbone](http://documentcloud.github.com/backbone/ "Backbone") o [Spine](http://spinejs.com/ "Spine"), che danno una struttura di base MVC per te, lasciando la sola roba interessante.

## Bonus: Distribuzione in 30 secondi con Heroku ##

[Heroku](http://heroku.com/ "Heroku") è un host web impressionante che gestisce tutti i server e che scala per te, permettendoci così di andare avanti con la roba interessante (la costruzione di incredibili applicazioni JavaScript). Hai bisogno di un account con Heroku per questo tutorial, ma la grande notizia è che il piano base è completamente gratuito. Anche se, tradizionalmente, Heroku è un host Ruby, hanno recentemente pubblicato il loro stack di Cedar, che include il supporto Node.

In primo luogo , abbiamo bisogno di creare un `Procfile`, che informa Heroku della nostra applicazione:

	echo "web: coffee index.coffee" > Procfile

Ora, se non l'hai già fatto, bisogna creare un repository git locale per l'applicazione:

	git init
	git add .
	git commit -m "First commit"

E ora per distribuire l'applicazione, useremo la gem `heroku` (che bisogna installare, se non l'hai già fatto).

	heroku create myAppName --stack cedar
	git push heroku master
	heroku open

Questo è tutto! Scherzi a parte, questo è tutto quello che c'è da fare. Hostare applicazioni NodeJS non è mai stato
più semplice.

## Librerie Aggiuntive ##

[Stitch](https://github.com/sstephenson/stitch "Stitch") e [Eco](https://github.com/sstephenson/eco "Eco") non sono le uniche librerie che è possibile utilizzare per la creazione di applicazioni CoffeeScript e Node. Ci sono una vasta gamma di alternative.

Ad esempio, quando si tratta di template, è possibile utilizzare [Mustache](http://mustache.github.com/ "Mustache"), [Jade](http://jade-lang.com/ "Jade"), o lascia la
HTML in puro CoffeeScript utilizzando [CoffeeKup](http://coffeekup.org/ "CoffeeKup").

Per servire l'applicazione, [Hem](http://github.com/maccman/hem "Hem") è una grande scelta, poiché supporta sia CommonJS
che moduli NPM, integrandosi perfettamente con il framework MVC CoffeeScript
[Spine](http://spinejs.com/ "Spine"). [node-browsify](https://github.com/substack/node-browserify "node-browserify") è un altro progetto simile. Oppure, per andare livello inferiore con integrazione [express](http://expressjs.com/ "express"), c'è [Connect-assets](https://github.com/TrevorBurnham/connect-assets "connect-assets") di Trevor Burnham.

Puoi trovare un elenco completo dei web framework CoffeeScript sul [wiki del progetto](https://github.com/jashkenas/coffee-script/wiki/Web-framework-plugins "wiki del progetto").